plugins {
  id "org.sonarqube" version "3.0"
}

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'

  group 'de.tesseraktor.tesserakt'
  version '1.0.0-SNAPSHOT'

  repositories {
    google()
    mavenLocal()
    mavenCentral()
    maven {
      url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
      url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }

  dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.6.1'
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.4.4'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
  }

  test {
    useJUnitPlatform()
  }

  java {
    withSourcesJar()
    withJavadocJar()
  }

  checkstyle {
    maxWarnings = 0
    ignoreFailures = false
    toolVersion '8.27'
    checkstyleTest.enabled = false
  }

  publishing {
    publications {
      maven(MavenPublication) {
        from(components.java)
      }
    }
    repositories {
      maven {
        name = "KlaukeEnterprisesPackages"
        def releasesRepoUrl = 'https://repository.klauke-enterprises.com/repository/maven-releases/'
        def snapshotsRepoUrl = 'https://repository.klauke-enterprises.com/repository/maven-snapshots/'
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = System.getenv('NEXUS_USERNAME')
          password = System.getenv('NEXUS_PASSWORD')
        }
      }
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/tesseraktor")
        credentials {
          username = System.getenv("GITHUB_PACKAGE_REGISTRY_USER")
          password = System.getenv("GITHUB_PACKAGE_REGISTRY_API_KEY")
        }
      }
    }
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "tesseraktor_tesserakt"
  }
}

task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  subprojects.each {
    sourceSets it.sourceSets.main
  }

  reports {
    xml.enabled = true
    html.enabled = true
    xml.destination = new File("${buildDir}/reports/jacoco/report.xml")
    html.destination = new File("${buildDir}/reports/jacoco/report.html")
  }
}
